import "apps/blas/scal/scal.fil";
import "apps/blas/util.fil";
import "primitives/reshape.fil";
import "primitives/core.fil";

// Performs a*x + y, where x,y are vectors and a is a scalar
// W: Width of nums
// N: Length of vectors
// M: Number of multipliers
// A: Number of adders
comp Axpy[W, N, M, A]<'G:II>(
  go:   interface['G],
  a:    ['G, 'G+1] W,
  x[N]: ['G, 'G+1] W,
  y[N]: ['G, 'G+1] W,
) -> (
  out[N]: ['G+L, 'G+L+1] W
) with {
  some L where L > 0;
  some II where II > 0;
} where W > 0,
        L > 0,
        N > 0,
        M > 0,
        A > 0,
        N % M == 0,
        M % A == 0
{
  
  let mult_uses = N / M;
  let add_uses = M / A;

  let add_latency = add_uses;
  let add_ii = 1;

  Mults := new Multipliers[W, M] in ['G, 'G + mult_uses*Mults::II];
  Adds := new Adders[W, A] in ['G + Mults::L, 'G + mult_uses*Mults::II + Mults::L + add_uses*add_ii + mult_uses*(add_uses-1)];

  let latency = (mult_uses-1)*Mults::II + Mults::L + add_uses-1 + (mult_uses-1)*(add_uses-1);
  L := latency;

  II := (mult_uses*Mults::II+Mults::L+add_uses*add_ii+mult_uses*(add_uses-1))-Mults::L; // compiler told me this

  bundle mult_out[mult_uses][M]: for<k> ['G+k*Mults::II+Mults::L, 'G+k*Mults::II+Mults::L+1] W;
  bundle a_bundle[mult_uses][M]: for<k> ['G+k*Mults::II, 'G+k*Mults::II+1] W;

  for j in 0..mult_uses {
    let mul_start = j*Mults::II;
    let mul_end = j*Mults::II + Mults::L;

    a_reg := new Shift[W, mul_start]<'G>(a);

    // use inputs
    if j == 0 {
      // fill bundle with `a`
      for i in 0..M {
        a_bundle{j}{i} = a;
      }
      m := Mults<'G+mul_start>(x{j*M..(j+1)*M}, a_bundle{j}{0..M});
      mult_out{j}{0..M} = m.out{0..M};
    }
    // register inputs and use
    else {
      // fill bundle with `a`
      for i in 0..M {
        a_bundle{j}{i} = a_reg.out;
      }
      x_reg := new Shift[W, mul_start, M]<'G>(x{j*M..(j+1)*M});
      m := Mults<'G+mul_start>(x_reg.out{0..M}, a_bundle{j}{0..M});
      mult_out{j}{0..M} = m.out{0..M};
    }

    // chunk multiply outputs based on how many adders we have
    for k in 0..add_uses {
      let offset = j*(add_uses-1);

      bundle add_in[A]: ['G+mul_end + k*add_ii + offset, 'G+mul_end + k*add_ii + offset + 1] W;

      y_reg := new Shift[W, mul_end + k*add_ii + offset, A]<'G>(y{(M*j)+(k*A)..(M*j)+(k+1)*A});

      mult_out_reg := new Shift[W, k*add_ii + offset, A]<'G+mul_end>(mult_out{j}{(k*A)..(k+1)*A});
      add_in{0..A} = mult_out_reg.out{0..A};

      a := Adds<'G+mul_end+k*add_ii+offset>(add_in{0..A}, y_reg.out{0..A});
      add_reg := new Shift[W, latency - mul_end - k*add_ii - offset, A]<'G+mul_end+k*add_ii+offset>(a.out{0..A});

      out{(M*j)+(k*A)..(M*j)+(k+1)*A} = add_reg.out{0..A};
    }
  }
}