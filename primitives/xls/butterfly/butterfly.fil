import "primitives/core.fil";

extern "impl.v" {
    comp Butterfly_Exp8_Mant23<'G:1>(
        clk: 1,
        in0_r: ['G, 'G+1] 32,
        in0_i: ['G, 'G+1] 32,
        in1_r: ['G, 'G+1] 32,
        in1_i: ['G, 'G+1] 32,
        twd_r: ['G, 'G+1] 32,
        twd_i: ['G, 'G+1] 32
    ) -> (
        out: ['G+4, 'G+5] 128
    );
}

comp ButterflyWrapper<'G:II>(
    go: interface['G],
    in0[2]: ['G, 'G+II] 32,
    in1[2]: ['G, 'G+II] 32,
    twd[2]: ['G, 'G+II] 32
) -> (
    out0[2]: ['G+L, 'G+L+1] 32,
    out1[2]: ['G+L, 'G+L+1] 32
) with {
    some II where II > 0;
    some L where L >= II;
} where
    L >= II,
    II > 0
{
    B := new Butterfly_Exp8_Mant23;
    b := B<'G>(in0{0}, in0{1}, in1{0}, in1{1}, twd{0}, twd{1});

    out0_r := new Slice[128, 127, 96]<'G+4>(b.out);
    out0_i := new Slice[128, 95, 64]<'G+4>(b.out);
    out1_r := new Slice[128, 63, 32]<'G+4>(b.out);
    out1_i := new Slice[128, 31, 0]<'G+4>(b.out);

    out0{0} = out0_r.out;
    out0{1} = out0_i.out;
    out1{0} = out1_r.out;
    out1{1} = out1_i.out;

    II := 1;
    L := 4;
}

comp ButterfliesWrapper[N]<'G:II>(
    go: interface['G],
    in[N*2][2]: ['G, 'G+II] 32,
    twiddle[N][2]: ['G, 'G+II] 32
) -> (
    out[N*2][2]: ['G+L, 'G+L+1] 32
) with {
    some II where II > 0;
    some L where L >= II;
} where
    L >= II,
    II > 0
{
    Bx := new ButterflyWrapper;
    L := Bx::L;
    II := Bx::II;

    for i in 0..N {
        b := new ButterflyWrapper<'G>(in{2*i}{0..2}, in{2*i+1}{0..2}, twiddle{i}{0..2});
        out{2*i}{0..2} = b.out0{0..2};
        out{2*i+1}{0..2} = b.out1{0..2};
    }
}




