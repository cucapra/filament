WHITESPACE = _{ " " | "\t" | NEWLINE }

// C-style comments
COMMENT = _{ ("//" ~ (!NEWLINE ~ ANY)* ~ NEWLINE) | "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

// Identifiers may begin with `_` or an ASCII character.
identifier = @{ ("_" | ASCII_ALPHA)+ ~ ("_" | ASCII_ALPHA | ASCII_DIGIT)* }

// Positive numbers
bitwidth = @{ ASCII_DIGIT+ }

char = { !"\"" ~ ANY }
string_lit = ${ "\"" ~ char* ~ "\"" }
import = _{
      "import" ~ string_lit ~ ";"
}
imports = { import* }

// ====== toplevel ======

file = {
  SOI
    ~ imports
    ~ comp_or_ext*
    ~ EOI
}

gt =  { ">" }
gte = { ">=" }
lt =  { "<" }
lte = { "<=" }
eq =  { "=" }
order_op = { gte | gt | lte | lt | eq }

constraint = {
  // port_width ~ order_op ~ port_width |
  time ~ order_op ~ time
}
constraints = {
  ("where" ~ (constraint ~ ("," ~ constraint)*))?
}

params = {
  ("[" ~ identifier ~ ("," ~ identifier)* ~ "]")?
}
signature = {
  identifier ~ params ~ abstract_var? ~ io ~ constraints
}
component = {
  "comp" ~ signature ~ "{" ~ command* ~ "}"
}
external = {
  "extern" ~ string_lit ~ "{" ~  ("comp" ~ signature ~ ";")*  ~ "}"
}

comp_or_ext = {
  component | external
}

// ====== Component signature ======

delay = {
  // port_width |
  bitwidth |
  time ~ "-" ~ "(" ~ time ~ ")"
}

event_with_delay = _{
  identifier ~ ":" ~ delay
}
event_bind = {
  "?" ~ event_with_delay ~ "=" ~ time |
  event_with_delay
}
abstract_var = {
  "<" ~ event_bind ~ ("," ~ event_bind)* ~ ">"
}

// Time
time = {
  identifier ~ ("+" ~ port_width)?
  | bitwidth
}

// Intervals
interval_range = {
 "@" ~ "[" ~ time ~ "," ~ time ~ "]"
}

// Ports
interface = {
  "@interface" ~ "[" ~ identifier ~ "]"
}
conc_or_var = {
  bitwidth | identifier
}
port_width = {
  conc_or_var ~ ("+" ~ conc_or_var)*
}
port_def = {
  (interval_range | interface)? ~ identifier ~ ":" ~ port_width
}

arrow = { "->" }

io = {
  "(" ~ ports? ~ ")" ~ arrow ~ "(" ~ ports? ~ ")"
}
ports = {
  port_def ~ ("," ~ port_def)* ~ ","?
}

// ====== Cell instance ======
conc_params = {
  ("[" ~ port_width ~ ("," ~ port_width)* ~ "]")?
}
instance = {
  identifier ~ ":=" ~ "new" ~ identifier ~ conc_params ~ invoke_args? ~ ";"
}

// Connections

guard = {
  port ~ ("|" ~ guard)?
}

connect = {
  port ~ "=" ~ (guard ~ "?")? ~ port ~ ";"
}

// ====== Invocations ==========

port = {
  identifier ~ "." ~ identifier
  | identifier
  | bitwidth
}

arguments = {
  "(" ~ ")"
  | "(" ~ port ~ ("," ~ port)* ~ ")"
}

time_args = {
  "<" ~ time ~ ("," ~ time)* ~ ">"
}

invoke_args = {
  time_args ~ arguments
}

invocation = {
  identifier ~ ":=" ~ identifier ~ invoke_args ~ ";"
  | identifier ~ ":=" ~ "invoke" ~ identifier ~ time_args ~ ";"
}

fsm = {
  "fsm" ~ identifier ~ "[" ~ bitwidth ~ "]" ~ "(" ~ port ~ ")" ~ ";"
}

// ========== Commands ==========
command = {
  instance | invocation | connect | fsm
}
