WHITESPACE = _{ " " | "\t" | NEWLINE }

// C-style comments
COMMENT = _{ ("//" ~ (!NEWLINE ~ ANY)* ~ NEWLINE) | "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

// Identifiers may begin with `_` or an ASCII character.
identifier = @{ ("_" | ASCII_ALPHA)+ ~ ("_" | ASCII_ALPHA | ASCII_DIGIT)* }

// Positive numbers
bitwidth = @{ ASCII_DIGIT+ }

char = { !"\"" ~ ANY }
string_lit = ${ "\"" ~ char* ~ "\"" }
import = _{
      "import" ~ string_lit ~ ";"
}
imports = { import* }

// ====== toplevel ======

file = {
  SOI
    ~ imports
    ~ comp_or_ext*
    ~ EOI
}

gt =  { ">" }
gte = { ">=" }
lt =  { "<" }
lte = { "<=" }
eq =  { "=" }
order_op = _{ gte | gt | lte | lt | eq }

constraint = {
  time ~ order_op ~ time
}
constraints = {
  ("where" ~ (constraint ~ ("," ~ constraint)*))?
}

params = {
  ("[" ~ identifier ~ ("," ~ identifier)* ~ "]")?
}
signature = {
  identifier ~ params ~ abstract_var? ~ io ~ constraints
}
component = {
  "component" ~ signature ~ "{" ~ command* ~ "}"
}
external = {
  "extern" ~ string_lit ~ "{" ~  ("component" ~ signature ~ ";")*  ~ "}"
}

comp_or_ext = {
  component | external
}

// ====== Component signature ======

event_bind = {
  "?" ~ identifier ~ "=" ~ time |
  identifier
}
abstract_var = {
  "<" ~ event_bind ~ ("," ~ event_bind)* ~ ">"
}

// Time
time = {
  | identifier ~ ("+" ~ bitwidth)?
}

// Intervals
interval_range = {
 "@" ~ "exact"? ~ "[" ~ time ~ "," ~ time ~ "]"
}
interval = _{
  interval_range ~ ("+" ~ interval_range)?
}

// Ports
phantom = { "@phantom" }
interface = {
  "@interface" ~ "[" ~ identifier ~ "," ~ time ~ "]" ~ phantom?
}
port_width = {
  bitwidth | identifier
}
port_def = {
  (interval | interface)? ~ identifier ~ ":" ~ port_width
}

arrow = { "->" }

io = {
  "(" ~ ports? ~ ")" ~ arrow ~ "(" ~ ports? ~ ")"
}
ports = {
  port_def ~ ("," ~ port_def)* ~ ","?
}

// ====== Cell instance ======
conc_params = {
  ("[" ~ bitwidth ~ ("," ~ bitwidth)* ~ "]")?
}
instance = {
  identifier ~ ":=" ~ "new" ~ identifier ~ conc_params ~ invoke_args? ~ ";"
}

// Connections

guard = {
  port ~ ("|" ~ guard)?
}

connect = {
  port ~ "=" ~ (guard ~ "?")? ~ port ~ ";"
}

// ====== Invocations ==========

port = {
  identifier ~ "." ~ identifier
  | identifier
  | bitwidth
}

arguments = {
  "(" ~ ")"
  | "(" ~ port ~ ("," ~ port)* ~ ")"
}

time_args = {
  "<" ~ time ~ ("," ~ time)* ~ ">"
}

invoke_args = {
  time_args ~ arguments
}

invocation = {
  identifier ~ ":=" ~ identifier ~ invoke_args ~ ";"
  | identifier ~ ":=" ~ "invoke" ~ identifier ~ time_args ~ ";"
}

fsm = {
  "fsm" ~ identifier ~ "[" ~ bitwidth ~ "]" ~ "(" ~ port ~ ")" ~ ";"
}

// ========== Commands ==========
command = {
  instance | invocation | connect | fsm
}
