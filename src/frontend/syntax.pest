WHITESPACE = _{ " " | "\t" | NEWLINE }

// C-style comments
COMMENT = _{ ("//" ~ (!NEWLINE ~ ANY)* ~ NEWLINE) | "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

// Identifiers may begin with `_` or an ASCII character.
identifier = @{ ("_" | ASCII_ALPHA)+ ~ ("_" | ASCII_ALPHA | ASCII_DIGIT)* }

// Positive numbers
bitwidth = @{ ASCII_DIGIT+ }

char = { !"\"" ~ ANY }
string_lit = ${ "\"" ~ char* ~ "\"" }
import = _{
      "import" ~ string_lit ~ ";"
}
imports = { import* }

// ====== toplevel ======

file = {
  SOI
    ~ imports
    ~ comp_or_ext*
    ~ EOI
}

gt =  { ">" }
gte = { ">=" }
lt =  { "<" }
lte = { "<=" }
eq =  { "=" }
order_op = _{ gte | gt | lte | lt | eq }

constraint = {
  time ~ order_op ~ time
}
constraints = {
  ("where" ~ (constraint ~ ("," ~ constraint)*))?
}

signature = {
  identifier ~ abstract_var? ~ io ~ constraints
}
component = {
  "component" ~ signature ~ "{" ~ command* ~ "}"
}
external = {
  "extern" ~ string_lit ~ "{" ~  ("component" ~ signature ~ ";")*  ~ "}"
}

comp_or_ext = {
  component | external
}

// ====== Component signature ======

abstract_var = {
  "<" ~ identifier ~ ("," ~ identifier)* ~ ">"
}

// Time
time_base = {
  bitwidth
  | ("max" ~ "(" ~ time ~ "," ~ time ~ ")")
  | identifier
}
time_expr = { (time_base ~ "+" ~ time) }
time = { time_expr | time_base }

// Intervals
interval_range = {
 "@" ~ "exact"? ~ "[" ~ time ~ "," ~ time ~ "]"
}
interval = _{
  interval_range ~ ("+" ~ interval_range)?
}

// Time sub expression
time_sub = {
  bitwidth
  | "|" ~ time ~ "-" ~ time ~ "|"
}

// Ports
interface = {
  "@interface" ~ "<" ~ identifier ~ "," ~ time_sub ~ ">"
}
port_def = {
  (interval | interface)? ~ identifier ~ ":" ~ bitwidth
}

arrow = { "->" }

io = {
  "(" ~ ports? ~ ")" ~ arrow ~ "(" ~ ports? ~ ")"
}
ports = {
  port_def ~ ("," ~ port_def)* ~ ","?
}

// ====== Cell instance ======
instance = {
  identifier ~ ":=" ~ "new" ~ identifier ~ ";"
}

// Connections

guard = {
  port ~ ("|" ~ guard)?
}

connect = {
  port ~ "=" ~ (guard ~ "?")? ~ port ~ ";"
}

// ====== Invocations ==========

port = {
  identifier ~ "." ~ identifier
  | identifier
  | bitwidth
}

arguments = {
  "(" ~ ")"
  | "(" ~ port ~ ("," ~ port)* ~ ")"
}

time_args = {
  "<" ~ time ~ ("," ~ time)* ~ ">"
}

invocation = {
  identifier ~ ":=" ~ identifier ~ time_args ~ arguments ~ ";"
  | identifier ~ ":=" ~ "invoke" ~ identifier ~ time_args ~ ";"
}

fsm = {
  "fsm" ~ identifier ~ "[" ~ bitwidth ~ "]" ~ "(" ~ port ~ ")" ~ ";"
}

// ========== Commands ==========
command = {
  instance | invocation | connect | fsm
}
