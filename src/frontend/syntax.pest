WHITESPACE = _{ " " | "\t" | NEWLINE }

// C-style comments
COMMENT = _{ ("//" ~ (!NEWLINE ~ ANY)* ~ NEWLINE) | "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

// Identifiers may begin with `_` or an ASCII character.
identifier = @{ ("_" | ASCII_ALPHA)+ ~ ("_" | ASCII_ALPHA | ASCII_DIGIT)* }

// Positive numbers
bitwidth = @{ ASCII_DIGIT+ }

// ====== toplevel ======

file = {
  SOI
    ~ comp_or_ext*
    ~ EOI
}



signature = {
  identifier ~ abstract_var? ~ io
}
component = {
  "component" ~ signature ~ "{" ~ command* ~ "}"
}
external = {
  "extern" ~ "component" ~ signature ~ ";"
}

comp_or_ext = {
  component | external
}

// ====== Component signature ======

abstract_var = {
  "<" ~ identifier ~ ("," ~ identifier)* ~ ">"
}

interval = {
  "@" ~ "[" ~ time ~ "," ~ time ~ "]"
}

time_base = {
  bitwidth
  | identifier
}
plus = { "+" }
max = { "max" }
time_expr = {
  (time_base ~ plus ~ time)
  | (max ~ "(" ~ time ~ "," ~ time ~ ")")
}
time = { time_expr | time_base }

port_def = {
  interval ~ identifier ~ ":" ~ (bitwidth | identifier)
}

arrow = { "->" }

io = {
  "(" ~ ports? ~ ")" ~ arrow ~ "(" ~ ports? ~ ")"
}
ports = {
  port_def ~ ("," ~ port_def)* ~ ","?
}

// ====== Cell instance ======
instance = {
  identifier ~ ":=" ~ "new" ~ identifier ~ ";"
}

// Connections

connect = {
  port ~ "=" ~ port ~ ";"
}

// ====== Invocations ==========

port = {
  identifier ~ "." ~ identifier
  | identifier
}

arguments = {
  "(" ~ ")"
  | "(" ~ port ~ ("," ~ port)* ~ ")"
}

time_args = {
  "<" ~ time ~ ("," ~ time)* ~ ">"
}
invocation_expr = {
  identifier ~ time_args ~ arguments
}

invocation = {
  identifier ~ ":=" ~ invocation_expr ~ ";"
}

// =========== When ============
when = {
  "when" ~ time ~ "{" ~ command* ~ "}"
}

// ========== Commands ==========
command = {
  instance | invocation | when | connect
}
