import "primitives/core.fil";

/// Implementation of a multiplier with initiation interval 1 and latency 3
component FastMult<G>(
  @interface[G, G+1] @phantom go_G: 1,
  @[G, G+1] left: 32,
  @[G, G+1] right: 32,
) -> (
  @[G+3, G+4] out: 32,
) {
  // First stage, register the inputs
  l := new Delay[32]<G>(left);
  r := new Delay[32]<G>(right);

  // Second stage, perform the computation and save it
  m := new MultComb[32]<G+1>(l.out, r.out);
  ot := new Delay[32]<G+1>(m.out);

  // Third stage, forward the value from temp out to out register
  final := new Delay[32]<G+2>(ot.out);

  // Connect the output to the out register
  out = final.out;
}


component main<G>(
  @interface[G, G+1] @phantom go: 1,
  @[G, G+1] left: 32,
  @[G, G+1] right: 32,
) -> (
  @[G+3, G+4] out: 32,
) {
  m := new FastMult<G>(left, right);
  out = m.out;
}