import "primitives/signed.fil";
import "primitives/math/complex.fil";
import "helpers.fil";

/// Toplevel FFT Module
comp CombFFT[NStages]<'G: 1>(
  in[NPoints*2]: ['G, 'G+1] W,
) -> (
  out[NPoints*2]: ['G, 'G+1] W
) with {
  let NPoints = pow2(NStages);
  // specifies the fixed point size
  let W = 32;
  let E = 8;
  let M = 23;
} where
  NStages > 0
{
  assume NPoints % 2 == 0;
  twiddle := new TwiddleFactor[NPoints]<'G>();
  bitrev := new BitRev[NStages, W]<'G>(in{0..NPoints*2});

  // run the inner fft
  fft := new _FFT_Inner[NStages, W, E, M]<'G>(bitrev.out{0..NPoints*2}, twiddle.out{0..NPoints});

  out{0..NPoints*2} = fft.out{0..NPoints*2};
}

/// Inner FFT module that takes twiddle factors as input
/// Assumes that input is already bit reversed here.
comp _FFT_Inner[NStages, W, E, ?M=W-E-1]<'G: 1>(
  in[NPoints*2]: ['G, 'G+1] W,
  twiddle[NPoints]: ['G, 'G+1] W,
) -> (
  out[NPoints*2]: ['G, 'G+1] W
) with {
  let NPoints = pow2(NStages);
} where
  NStages > 0,
  W == E + M + 1,
  E > 1,
  M > 1
{
  if NStages == 1 {
    // A single butterfly
    butterfly := new Butterfly[W, E, M]<'G>(in{0..2}, in{2..4}, twiddle{0..2});
    out{0..2} = butterfly.out0{0..2};
    out{2..4} = butterfly.out1{0..2};
  } else {
    // twiddle factors for the smaller fft
    bundle inner_twiddle[NPoints/2]: ['G, 'G+1] W;
    // hook up every other fft to this fft.
    assume pow2(NStages-1) == NPoints/2;
    assume pow2(NStages-2) == NPoints/4;
    for t in 0..NPoints/4 {
      inner_twiddle{t*2} = twiddle{t*4};
      inner_twiddle{t*2+1} = twiddle{t*4+1};
    } 
    // Run the two smaller FFTs
    // This does the first `N-1` Stages of the FFT for us.
    fft1 := new _FFT_Inner[NStages-1, W, E, M]<'G>(in{0..NPoints}, inner_twiddle{0..NPoints/2});
    fft2 := new _FFT_Inner[NStages-1, W, E, M]<'G>(in{NPoints..NPoints*2}, inner_twiddle{0..NPoints/2});

    // Combine the two FFTs in the last stage
    for i in 0..pow2(NStages-1) {
      butterfly := new Butterfly[W, E, M]<'G>(fft1.out{i*2..i*2+2}, fft2.out{i*2..i*2+2}, twiddle{2*i..2*i+2});
      out{i*2..i*2+2} = butterfly.out0{0..2};
      out{i*2+NPoints..i*2+NPoints+2} = butterfly.out1{0..2};
    }
  }
}